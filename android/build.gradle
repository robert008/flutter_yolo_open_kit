// The Android Gradle Plugin builds the native code with the Android NDK.

group = "com.example.flutter_yolo_open_kit"
version = "1.0"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        // The Android Gradle Plugin knows how to build native code with the NDK.
        classpath("com.android.tools.build:gradle:8.7.0")
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"

// Pre-built native libraries configuration
def jniLibsDir = file("src/main/jniLibs")
def arm64Dir = file("src/main/jniLibs/arm64-v8a")
def releaseVersion = "v1.0.0"
def downloadUrl = "https://github.com/robert008/flutter_yolo_open_kit/releases/download/${releaseVersion}/android-jniLibs.zip"

task downloadNativeLibraries {
    doLast {
        // Check if native libraries already exist
        if (arm64Dir.exists() && file("${arm64Dir}/libonnxruntime.so").exists()) {
            println "Android native libraries already exist"
            return
        }

        println "Downloading Android native libraries..."
        println "URL: ${downloadUrl}"

        def zipFile = file("${buildDir}/jniLibs.zip")
        zipFile.parentFile.mkdirs()

        ant.get(src: downloadUrl, dest: zipFile, skipexisting: 'false')

        jniLibsDir.mkdirs()
        ant.unzip(src: zipFile, dest: file("src/main"))

        zipFile.delete()

        println "Android native libraries downloaded successfully"
    }
}

// Uncomment the following line to enable automatic download
// preBuild.dependsOn downloadNativeLibraries

android {
    namespace = "com.example.flutter_yolo_open_kit"

    // Bumping the plugin compileSdk version requires all clients of this plugin
    // to bump the version in their app.
    compileSdk = 35

    // Use the NDK version
    // declared in /android/app/build.gradle file of the Flutter project.
    // Replace it with a version number if this plugin requires a specific NDK version.
    // (e.g. ndkVersion "23.1.7779620")
    ndkVersion = android.ndkVersion

    // Invoke the shared CMake build with the Android Gradle Plugin.
    externalNativeBuild {
        cmake {
            path = "../src/CMakeLists.txt"

            // The default CMake version for the Android Gradle Plugin is 3.10.2.
            // https://developer.android.com/studio/projects/install-ndk#vanilla_cmake
            //
            // The Flutter tooling requires that developers have CMake 3.10 or later
            // installed. You should not increase this version, as doing so will cause
            // the plugin to fail to compile for some customers of the plugin.
            // version "3.10.2"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    defaultConfig {
        minSdk = 21
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }
}
